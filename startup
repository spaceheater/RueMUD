#! /bin/csh -f
# Written by Furey.
# With additions from Tony.

# Set the port number.
set port = 4000
if ( "$1" != "" ) set port="$1"

# Set limits.
nohup
nice
if ( -e shutdown.txt ) rm -f shutdown.txt

while ( 1 )
    # If you want to have logs in a different directory,
    #   change the 'set logfile' line to reflect the directory name.
    set index = 1000
    while ( 1 )
	set logfile = ./$index.log
	if ( ! -e $logfile ) break
	@ index++
    end

    # Record starting time
    date > $logfile

    # Run mud.
    ./murk $port >&! $logfile

    # Restart, giving old connections a chance to die.
    if ( -e shutdown.txt ) then
	rm -f shutdown.txt
	exit 0
    endif
    sleep 15
end
